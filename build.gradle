plugins {
    id "java-library"
    id "org.sonarqube" version "6.2.0.5505"
    id 'com.github.spotbugs' version '6.1.13'
    id "com.diffplug.spotless" version "7.0.4"
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

sonar {
    properties {
        property "sonar.projectKey", "microsoft_kiota-java"
        property "sonar.organization", "microsoft"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.projectDir}/components/**/reports/jacoco/test/jacocoTestReport.xml"
    }
}

subprojects {
    apply plugin: 'org.sonarqube'
}

repositories {
    mavenCentral()
}

nexusPublishing {
    repositories {
        sonatype {
            if (project.rootProject.file('local.properties').exists()) {
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                username = properties.getProperty('sonatypeUsername')
                password = properties.getProperty('sonatypePassword')
            }
        }
    }
}

group = project.property('mavenGroupId')
version = "${mavenMajorVersion}.${mavenMinorVersion}.${mavenPatchVersion}${mavenCentralSnapshotArtifactSuffix}"
